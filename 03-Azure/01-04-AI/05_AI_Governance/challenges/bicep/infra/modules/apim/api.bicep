@description('The name of the API')
@minLength(1)
@maxLength(63)
param apiName string

@description('The display name of the API')
@minLength(1)
@maxLength(63)
param apiDispalyName string

@description('The contents of the OpenAPI definition')
@minLength(1)
param openApiSpecification string

@description('The XML Policy document for the API')
@minLength(1)
param policyDocument string

@description('The name of the API Management service to deploy the API to.')
@minLength(1)
param serviceName string

@description('The API description (if blank, use the name of the API)')
param apiDescription string = ''

@description('The relative path for the API (if different to the API name)')
param path string = ''

@description('The (optional) service URL')
param serviceUrl string = ''

@description('Set to true if a subscription is required')
param subscriptionRequired bool = true

@description('API Revision number. Default is 1')
param apiRevision string = '1'

@description('Ability to override the subscription key name. Default is Ocp-Apim-Subscription-Key')
param subscriptionKeyName string = ''

param enableAPIDeployment bool = true

param apimLoggerId string = 'azuremonitor'

param enableAPIDiagnostics bool

// Assume the content format is JSON format if the ending is .json - otherwise, it's YAML
var contentFormat = startsWith(openApiSpecification, '{') ? 'openapi+json' : 'openapi'

@description('The type of the API')
@allowed([
  'http'
  'soap'
  'graphql'
  'websocket'
])
param apiType string = 'http'

@description('The protocols supported by the API')
@allowed([
  'http'
  'https'
  'ws'
  'wss'
])
param apiProtocols array = [
  'https'
]

resource apimService 'Microsoft.ApiManagement/service@2022-08-01' existing = {
  name: serviceName
}

var isWebSotcketAPI = contains(apiProtocols, 'ws') || contains(apiProtocols, 'wss')

var logSettings = {
  headers: [ 'Content-type', 'User-agent', 'x-ms-region', 'x-ratelimit-remaining-tokens' , 'x-ratelimit-remaining-requests' ]
  body: { bytes: 0 }
}

resource apiDefinition 'Microsoft.ApiManagement/service/apis@2022-08-01' = if(enableAPIDeployment && !isWebSotcketAPI) {
  name: apiName
  parent: apimService
  properties: {
    path: (path == '') ? apiName : path
    apiRevision: apiRevision
    description: (apiDescription == '') ? apiName : apiDescription
    displayName: apiDispalyName
    format: (openApiSpecification != 'NA') ? contentFormat : null
    value: (openApiSpecification != 'NA') ? openApiSpecification : null
    subscriptionRequired: subscriptionRequired
    subscriptionKeyParameterNames: {
      header: empty(subscriptionKeyName) ? 'Ocp-Apim-Subscription-Key' : subscriptionKeyName
    }
    type: apiType
    protocols: apiProtocols
    serviceUrl: (serviceUrl == '') ? 'https://to-be-replaced-by-policy' : serviceUrl
  }
}


// If the API is a WebSocket API, we need to create it differently (both the api and policy)
resource apiDefinitionWebSocket 'Microsoft.ApiManagement/service/apis@2022-08-01' = if(enableAPIDeployment && isWebSotcketAPI) {
  name: apiName
  parent: apimService
  properties: {
    path: (path == '') ? apiName : path
    apiRevision: apiRevision
    description: (apiDescription == '') ? apiName : apiDescription
    displayName: apiDispalyName
    subscriptionRequired: subscriptionRequired
    subscriptionKeyParameterNames: {
      header: empty(subscriptionKeyName) ? 'Ocp-Apim-Subscription-Key' : subscriptionKeyName
    }
    type: apiType
    protocols: apiProtocols
    serviceUrl: (serviceUrl == '') ? 'https://to-be-replaced-by-policy' : serviceUrl
  }
}

resource apiDefinitionWebSocketHandshake 'Microsoft.ApiManagement/service/apis/operations@2024-06-01-preview' existing = if(enableAPIDeployment && isWebSotcketAPI && policyDocument != 'NA') {
  name: 'onHandshake'
  parent: apiDefinitionWebSocket
}

// https://learn.microsoft.com/azure/templates/microsoft.apimanagement/service/apis/policies
resource apiDefinitionWebSocketHandshakePolicy 'Microsoft.ApiManagement/service/apis/operations/policies@2024-06-01-preview' = if(enableAPIDeployment && isWebSotcketAPI && policyDocument != 'NA') {
  name: 'policy'
  parent: apiDefinitionWebSocketHandshake
  properties: {
    format: 'rawxml'
    value: policyDocument
  }
}


resource apiPolicy 'Microsoft.ApiManagement/service/apis/policies@2022-08-01' = if(enableAPIDeployment && policyDocument != 'NA' && !isWebSotcketAPI) {
  name: 'policy'
  parent: apiDefinition
  properties: {
    format: 'rawxml'
    value: policyDocument
  }
}

resource apiDiagnostics 'Microsoft.ApiManagement/service/apis/diagnostics@2024-06-01-preview' = if(enableAPIDiagnostics && enableAPIDeployment && !isWebSotcketAPI) {
  parent: apiDefinition
  name: 'azuremonitor'
  properties: {
    alwaysLog: 'allErrors'
    verbosity: 'Information'
    logClientIp: true
    loggerId: resourceId(resourceGroup().name, 'Microsoft.ApiManagement/service/loggers', apimService.name, apimLoggerId)
    sampling: {
      samplingType: 'fixed'
      percentage: json('100')
    }
    frontend: {
      request: {
        headers: []
        body: {
          bytes: 0
        }
      }
      response: {
        headers: []
        body: {
          bytes: 0
        }
      }
    }
    backend: {
      request: {
        headers: []
        body: {
          bytes: 0
        }
      }
      response: {
        headers: []
        body: {
          bytes: 0
        }
      }
    }
    largeLanguageModel: {
      logs: 'enabled'
      requests: {
        messages: 'all'
        maxSizeInBytes: 262144
      }
      responses: {
        messages: 'all'
        maxSizeInBytes: 262144
      }
    }
  }
}

resource apiDiagnosticsAppInsights 'Microsoft.ApiManagement/service/apis/diagnostics@2022-08-01' = if (enableAPIDiagnostics && enableAPIDeployment && !isWebSotcketAPI) {
  name: 'applicationinsights'
  parent: apiDefinition
  properties: {
    alwaysLog: 'allErrors'
    httpCorrelationProtocol: 'W3C'
    logClientIp: true
    loggerId: resourceId(resourceGroup().name, 'Microsoft.ApiManagement/service/loggers', apimService.name, 'appinsights-logger')
    metrics: true
    verbosity: 'Information'
    sampling: {
      samplingType: 'fixed'
      percentage: 100
    }
    frontend: {
      request: logSettings
      response: logSettings
    }
    backend: {
      request: logSettings
      response: logSettings
    }
  }
}

output id string = (enableAPIDeployment) ? apiDefinition.id : ''
output path string = (enableAPIDeployment) ? apiDefinition.properties.path : ''
